prototype(Neos.Presentation:Module.ImageCollage) < prototype(Neos.Fusion:Component) {

    @styleguide {
        title = "Image Collage"
        props {
            headline.text = 'Projects created with Neos CMS'
            description.text = 'Lorem ipsum dolor sit amet, consetetur sadipscing elitr, sed diam nonumy eirmod tempor invidunt ut labore et dolore magna aliquyam erat, sed diam voluptua.'
            squareIcons = Neos.Fusion:Map {
                items = ${[
                    ['Lorem ipsum', 'heart', 'purple'],
                    ['dolor sit', 'circle', 'yellow'],
                    ['amet, consetetur', 'clock', 'lightblue']
                ]}
                itemName = 'item'
                itemRenderer = Neos.Fusion:DataStructure {
                    text = ${item[0]}
                    iconName = ${item[1]}
                    color = ${item[2]}
                }
            }
            images = Neos.Fusion:Map {
                items = ${['allplan', 'berchtesgadener', 'heineken', 'hitachi', 'hoerbiger', 'hogast', 'kvh', 'siga', 't3n']}
                itemName = 'item'
                itemRenderer = Neos.Fusion:DataStructure {
                    src = ${StaticResource.uri('Neos.Presentation', 'Public/Assets/Styleguide/cases/' + item + '.png')}
                    alternativeText = ${'Case ' + props.item}
                }
            }
        }
    }

    @propTypes {
        headline = PropTypes:DataStructure
        description = PropTypes:DataStructure
        squareIcons = PropTypes:Array {
            type = PropTypes:DataStructure {
                src = PropTypes:String
                alternativeText = PropTypes:String
            }
        }
        images = PropTypes:Array {
            type = PropTypes:DataStructure {
                text = PropTypes:String
                iconName = PropTypes:String
                color = PropTypes:String
            }
        }
    }

    renderer = afx`
      <Neos.Presentation:Background variant="gradient" class="overflow-clip">
        <Neos.Presentation:Spacing size y>
          <div class="grid md:grid-cols-2 items-center">
            <Neos.Presentation:Headline {...props.headline} display="headline-lg" />
            <Neos.Presentation:Paragraph {...props.description} display="lead" />
          </div>

          <figure class="relative h-[1400px]" x-data="collage" "x-on:resize.window.debounce"="processElements">
            <Neos.Fusion:Loop items={props.images}>
              <img class="opacity-0 transition-all absolute w-[128px] h-auto" src={item.src} alt={item.alternativeText} />
            </Neos.Fusion:Loop>
            <Neos.Fusion:Loop items={props.squareIcons}>
              <Neos.Fusion:Augmenter class="absolute z-1 opacity-0 transition-all">
                <Neos.Presentation:SquareIcon {...item}/>
              </Neos.Fusion:Augmenter>
            </Neos.Fusion:Loop>
          </figure>

        </Neos.Presentation:Spacing>
      </Neos.Presentation:Background>
    `
}
