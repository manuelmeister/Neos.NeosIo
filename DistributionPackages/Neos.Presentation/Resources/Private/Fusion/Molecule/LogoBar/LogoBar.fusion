prototype(Neos.Presentation:LogoBar) < prototype(Neos.Fusion:Component) {

    @styleguide {
        title = 'Logo-Bar'
        props {
            assets = Neos.Fusion:Map {
                items = ${['solarwatt.svg', 'heineken.svg', 'swisscom.svg', 't3n.png']}
                itemRenderer = Neos.Fusion:DataStructure {
                    src = ${StaticResource.uri('Neos.Presentation', 'Public/Assets/Styleguide/logos/' + item)}
                    alternativeText = ${'Logo ' + props.item}
                }
            }
        }
    }

    @propTypes {
        assets = PropTypes:Array {
            type = PropTypes:DataStructure {
                src = PropTypes:String
                alternativeText = PropTypes:String
            }
        }
        grayscale = PropTypes:Bool
    }

    assets = null
    grayscale = false

    renderer = afx`
      <div class={['h-10 md:h-16 w-full inline-flex flex-nowrap overflow-x-hidden', props.grayscale ? 'grayscale' : null]}
           @if={props.assets && Array.length(props.assets)}
           x-data="LogoBar">
        <ul x-ref="logos"
            class="flex item-list gap-11 items-center justify-center md:justify-start pr-11 [&_li]:mx-8 [&_img]:max-w-none animate-infinite-scroll">
          <Neos.Fusion:Loop items={props.assets} itemName="asset">
            <li><img @if={asset.src} class="max-h-[75%]" alt={asset.alternativeText} src={asset.src}/></li>
          </Neos.Fusion:Loop>
        </ul>
      </div>
    `
}
